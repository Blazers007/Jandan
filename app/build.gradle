apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'checkstyle'
apply plugin: 'android-apt'
apply plugin: 'im.fir.plugin.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "com.blazers.jandan"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        dataBinding {
            enabled true
        }
    }

    signingConfigs {
        config {
            keyAlias project.keyAlias
            keyPassword project.keyPassword
            storeFile file(project.storeFile)
            storePassword project.storePassword
        }
    }

    buildTypes {
        debug {
            debuggable true
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.config
            manifestPlaceholders = [
                    UMENG_APPKEY : '562ec5f467e58e1679000f52',
                    UMENG_CHANNEL: getChannel()
            ]
        }
        release {
            debuggable false
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    UMENG_APPKEY : '562ec5f467e58e1679000f52',
                    UMENG_CHANNEL: getChannel()
            ]
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }

    productFlavors {

    }
}

fir {
    apiToken '88f2f9a3ca9a001493aed0caba87a926'
}

def getChannel() {
    "CoolApk"
}

checkstyle {
    toolVersion '6.1.1'
    showViolations true
}

dependencies {
    // 依赖库版本
    def supportVersion = '25.0.1'
    def frescoVersion = '0.14.1'
    def butterKnifeVersion = '8.4.0'
    def retrofitVersion = '2.1.0'
    def permissionVersion = '2.2.0'

    // Test compile

    // Jars
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Support
    compile group: 'com.android.support', name: 'support-v4',      version: supportVersion
    compile group: 'com.android.support', name: 'appcompat-v7',    version: supportVersion
    compile group: 'com.android.support', name: 'recyclerview-v7', version: supportVersion
    compile group: 'com.android.support', name: 'design',          version: supportVersion

    // ZHY
    compile group: 'com.zhy', name: 'percent-support-extends', version: '1.1.1'

    // Fresco
    compile group: 'com.facebook.fresco', name: 'fresco',        version: frescoVersion
    compile group: 'com.facebook.fresco', name: 'animated-gif',  version: frescoVersion
    compile group: 'com.facebook.fresco', name: 'animated-webp', version: frescoVersion

    // Fresco Pipeline
    compile group: 'com.facebook.fresco', name: 'imagepipeline-okhttp3', version: frescoVersion
    // WEBP-SUPPORT
//    compile "com.facebook.fresco:webpsupport:${frescoVersion}"

    // Once
    compile group: 'com.jonathanfinerty.once', name: 'once', version: '1.2.1'

    // ButterKnife
    compile group: 'com.jakewharton', name: 'butterknife',          version: butterKnifeVersion
    apt     group: 'com.jakewharton', name: 'butterknife-compiler', version: butterKnifeVersion

    // OkHttp & Retrofit
    compile group: 'com.squareup.okhttp3',  name: 'okhttp',          version: '3.4.1'
    compile group: 'com.squareup.retrofit2', name: 'retrofit',       version: retrofitVersion
    compile group: 'com.squareup.retrofit2', name: 'converter-gson', version: retrofitVersion
    compile group: 'com.squareup.retrofit2', name: 'adapter-rxjava', version: retrofitVersion

    // Permission Dispatcher
    compile group: 'com.github.hotchemi', name: 'permissionsdispatcher',           version: permissionVersion
    apt     group: 'com.github.hotchemi', name: 'permissionsdispatcher-processor', version: permissionVersion

    // RxJava
    compile group: 'io.reactivex', name: 'rxandroid', version: '1.2.1'
    compile group: 'io.reactivex', name: 'rxjava', version: '1.1.6'

    // 动画库
    compile group: 'com.github.thunderrise', name: 'android-TNRAnimationHelper', version: '-SNAPSHOT'

    // Material Dialog
    compile group: 'com.afollestad.material-dialogs', name: 'core', version: '0.9.0.2'

    // Gson
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'

    // U萌统计
    compile group: 'com.umeng.analytics', name: 'umeng-analytics', version: 'latest.integration'

    // Material Fav Button
    compile group: 'com.github.ivbaranov', name: 'materialfavoritebutton', version: '0.1.2'

    // SpannableText
    compile group: 'com.ms-square', name: 'expandableTextView', version: '0.1.4'

    // JAVA8 Stream
    compile group: 'com.annimon', name:'stream', version: '1.1.3'

    // jsoup
    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.1'

    testCompile 'junit:junit:4.12'
}
